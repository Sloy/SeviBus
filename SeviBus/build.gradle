buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath "com.fernandocejas.frodo:frodo-plugin:0.8.1"
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://raw.github.com/ichigotake/ColorPicker/migrate-to-gradle/repository' }

}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'com.github.triplet.play'


def myVersionCode = 91
def myVersionName = "3.7.2-beta2"

android {
    signingConfigs {
        release {
            storeFile file("../sevibus.keystore")
        }
        debug {
            storeFile file("../debug.keystore")
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId 'com.sloy.sevibus'
        minSdkVersion 14
        targetSdkVersion 22
        versionCode myVersionCode
        versionName myVersionName
        archivesBaseName = "SeviBus-" + versionName
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
        }

        release {
            minifyEnabled false
            signingConfig = signingConfigs.release
        }
    }
    productFlavors {
        sevilla {}
        barna {}
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    def supportVersion = "23.1.1"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:percent:$supportVersion"

    def playServicesVersion = "9.0.0"
    compile "com.google.android.gms:play-services-location:$playServicesVersion"
    compile "com.google.android.gms:play-services-maps:$playServicesVersion"
    compile "com.google.android.gms:play-services-gcm:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"

    def firebaseVersion = "9.0.0"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-analytics:$firebaseVersion"
    compile "com.google.firebase:firebase-config:$firebaseVersion"

    compile 'com.sloydev:gallego:1.0.0'
    compile 'io.reactivex:rxjava:1.0.16'
    compile 'io.reactivex:rxjava-math:1.0.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.j256.ormlite:ormlite-android:4.42'
    compile 'com.j256.ormlite:ormlite-core:4.42'
    compile 'net.sourceforge.htmlcleaner:htmlcleaner:2.7'
    compile('com.fourmob.colorpicker:colorpicker:1.0.0') {
        exclude module: 'support-v4';
    }
    compile 'de.cketti.library.changelog:ckchangelog:1.2.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.facebook.stetho:stetho:1.2.0'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.3@aar') {
        transitive = true;
    }
    compile 'com.android.support:multidex:1.0.1'

    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.github.justzak:dilatingdotsprogressbar:1.0.1'
    compile 'de.cketti.mailto:email-intent-builder:1.0.0'
    compile 'com.artjimlop:chernobyl:1.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

play {
    jsonFile = file('gplay-secret.json')
    track = 'alpha'
//    userFraction = 0.005
}


def (releaseKeystorePassword, releaseKeyAlias, releaseKeyPassword) = signingConfigFromFile()
if (releaseKeystorePassword != null && releaseKeyAlias != null && releaseKeyPassword != null) {
    android.signingConfigs.release.storePassword = releaseKeystorePassword
    android.signingConfigs.release.keyAlias = releaseKeyAlias
    android.signingConfigs.release.keyPassword = releaseKeyPassword
} else {
    println "No se han encontrado las credenciales necesarias para poder firmar el apk"
    android.buildTypes.release.signingConfig = null
}

def signingConfigFromFile() {
    println '-> signingConfigFromFile'
    def Properties props = new Properties()
    def propFile = file('signing.properties')

    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props != null &&
                props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') &&
                props.containsKey('KEY_PASSWORD')) {

            def keystorePassword = props['STORE_PASSWORD']
            def keyAlias = props['KEY_ALIAS']
            def keyPassword = props['KEY_PASSWORD']

            return [keystorePassword, keyAlias, keyPassword]
        } else {
            println 'signing.properties found but some entries are missing'
        }
    } else {
        println 'signing.properties not found'
    }
    return [null, null, null]
}

apply plugin: 'com.google.gms.google-services'